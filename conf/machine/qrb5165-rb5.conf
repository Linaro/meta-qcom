#@TYPE: Machine
#@NAME: RB5 Robotics platform
#@DESCRIPTION: Machine configuration for the RB5 development board, with Qualcomm Snapdragon 865 QRB5165.

require conf/machine/include/qcom-sm8250.inc

MACHINE_FEATURES = "usbhost usbgadget alsa screen wifi bluetooth ext2"

KERNEL_IMAGETYPE ?= "Image.gz"
KERNEL_DEVICETREE ?= "qcom/qrb5165-rb5.dtb"

SERIAL_CONSOLE ?= "115200 ttyMSM0"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    kernel-modules \
    firmware-qcom-rb5 \
    ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'linux-firmware-ath11k wireless-regdb-static', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'bluetooth', 'linux-firmware-qca', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-msm', '', d)} \
"
# linux-firmware-qcom-adreno-a650 

# 'userdata' is sda8 with older firmware/GPT and sda6 with newer firmware.
# Allow kernel to resolve it on it's own.  Wipe it and use for our build.
QCOM_BOOTIMG_ROOTFS ?= "PARTLABEL=userdata"

# UFS partitions setup with 4096 logical sector size
EXTRA_IMAGECMD_ext4 += " -b 4096 "
