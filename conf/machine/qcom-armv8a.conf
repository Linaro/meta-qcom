require conf/machine/include/qcom-common.inc
require conf/machine/include/arm/arch-armv8a.inc

MACHINE_FEATURES = "usbhost usbgadget alsa screen wifi bluetooth ext2"

# UFS partitions in 820/845/RB5 setup with 4096 logical sector size
EXTRA_IMAGECMD:ext4 += " -b 4096 "

PREFERRED_PROVIDER_virtual/bootloader ??= "u-boot-qcom"

UBOOT_CONFIG ??= "qcs6490-rb3gen2 sdm845-db845c"
UBOOT_CONFIG[qcs6490-rb3gen2] = "qcm6490_defconfig"
UBOOT_CONFIG[sdm845-db845c] = "qcom_defconfig"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"

# Support for dragonboard{410, 820, 845}c, rb5
KERNEL_IMAGETYPE ?= "Image.gz"

# UKI generation needs uncompressed Kernel image
KERNEL_IMAGETYPES = "Image"

SERIAL_CONSOLE ?= "115200 ttyMSM0"
KERNEL_DEVICETREE ?= " \
    qcom/apq8016-sbc.dtb \
    qcom/apq8096-db820c.dtb \
    qcom/sdm845-db845c.dtb \
    qcom/qcs404-evb-4000.dtb \
    qcom/qrb2210-rb1.dtb \
    qcom/qrb4210-rb2.dtb \
    qcom/qrb5165-rb5.dtb \
    qcom/sa8775p-ride.dtb \
    qcom/sm8450-hdk.dtb \
"

# These DT are not upstreamed (yet) and currenty exist only as a patches against linux-yocto
KERNEL_DEVICETREE:append:pn-linux-yocto = " \
    qcom/qcm6490-idp.dtb \
    qcom/qcs6490-rb3gen2.dtb \
"

QCOM_BOOTIMG_PAGE_SIZE[apq8016-sbc] ?= "2048"
QCOM_BOOTIMG_ROOTFS ?= "/dev/sda1"
QCOM_BOOTIMG_ROOTFS[apq8016-sbc] ?= "/dev/mmcblk0p14"
QCOM_BOOTIMG_ROOTFS[sm8450-hdk] ?= "PARTLABEL=userdata"
QCOM_BOOTIMG_ROOTFS[qcs404-evb-4000] ?= "/dev/mmcblk0p27"
QCOM_BOOTIMG_ROOTFS[qrb2210-rb1] ?= "PARTLABEL=userdata"
QCOM_BOOTIMG_ROOTFS[qrb4210-rb2] ?= "PARTLABEL=userdata"
QCOM_BOOTIMG_ROOTFS[qcs6490-rb3gen2] ?= "PARTLABEL=system"
SD_QCOM_BOOTIMG_ROOTFS[apq8016-sbc] ?= "/dev/mmcblk1p7"
KERNEL_CMDLINE_EXTRA[sdm845-db845c] ?= "clk_ignore_unused pd_ignore_unused"

# Userspace tools
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    pd-mapper \
    qrtr \
    rmtfs \
    tqftpserv \
"

MACHINE_EXTRA_RRECOMMENDS += " \
    fastrpc \
"

# Modules and firmware for all supported machines
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    kernel-modules \
    ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-msm', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'wireless-regdb-static', '', d)} \
    packagegroup-firmware-dragonboard410c \
    packagegroup-firmware-dragonboard820c \
    packagegroup-firmware-dragonboard845c \
    packagegroup-firmware-rb1 \
    packagegroup-firmware-rb2 \
    packagegroup-firmware-rb3gen2 \
    packagegroup-firmware-rb5 \
"
