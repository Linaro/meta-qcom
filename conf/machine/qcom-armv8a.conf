require conf/machine/include/qcom-common.inc
require conf/machine/include/arm/arch-armv8a.inc

MACHINE_FEATURES = "usbhost usbgadget alsa screen wifi bluetooth ext2"

# UFS partitions in 820/845/RB5 setup with 4096 logical sector size
EXTRA_IMAGECMD_ext4 += " -b 4096 "

# Support for dragonboard{410, 820, 845}c
KERNEL_IMAGETYPE ?= "Image.gz"
KERNEL_DEVICETREE ?= "qcom/apq8016-sbc.dtb qcom/apq8096-db820c.dtb qcom/sdm845-db845c.dtb"
SERIAL_CONSOLE ?= "115200 ttyMSM0"

# Userspace tools
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    ${@bb.utils.contains('DISTRO_FEATURES', 'bluez5', 'bluez5-noinst-tools', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-msm', '', d)} \
    pd-mapper \
    qrtr \
    rmtfs \
    tqftpserv \
"

MACHINE_EXTRA_RRECOMMENDS += " \
    fastrpc \
"

# Modules and firmware for all supported machines
MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    kernel-modules \
    firmware-qcom-dragonboard410c \
    firmware-qcom-dragonboard820c \
    firmware-qcom-dragonboard845c \
    firmware-qcom-rb5 \
    ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'linux-firmware-ath10k linux-firmware-qcom-sdm845-modem', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'bluetooth', 'linux-firmware-qca linux-firmware-qcom-sdm845-modem', '', d)} \
    linux-firmware-qcom-sdm845-audio \
    linux-firmware-qcom-sdm845-compute \
    ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'linux-firmware-qcom-adreno-a3xx linux-firmware-qcom-adreno-a530 linux-firmware-qcom-adreno-a630', '', d)} \
    linux-firmware-qcom-venus-1.8 \
    linux-firmware-qcom-venus-4.2 \
    linux-firmware-qcom-venus-5.2 \
"
