#@TYPE: Machine
#@NAME: dragonboard-410c
#@DESCRIPTION: Machine configuration for the DragonBoard 410c (96boards), with Qualcomm Snapdragon 410 APQ8016.

require conf/machine/include/qcom-apq8016.inc
require conf/machine/include/arm/arch-armv8.inc

MACHINE_FEATURES = "usbhost usbgadget alsa screen wifi bluetooth ext2"

KERNEL_IMAGETYPE = "Image.gz"
KERNEL_DEVICETREE = "qcom/apq8016-sbc.dtb"

SERIAL_CONSOLE = "115200 ttyMSM0"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    kernel-modules \
    ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-msm', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'wcnss-config wcnss-start', '', d)} \
    ${@bb.utils.contains('DISTRO_FEATURES', 'bluez5', 'bluez5-noinst-tools', '', d)} \
    firmware-qcom-dragonboard410c \
"

QCOM_BOOTIMG_ROOTFS ?= "mmcblk0p10"

# Define rootfs partiton (kernel argument)
SD_QCOM_BOOTIMG_ROOTFS ?= "mmcblk1p7"

# Assemble SD card
IMAGE_FSTYPES += "wic.gz"
WKS_FILE = "dragonboard410c-sd.wks"
WKS_FILE_DEPENDS = "firmware-qcom-dragonboard410c-bootloader-sdcard"
